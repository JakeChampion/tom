use super::Events;
use *;

grammar<E>(events: &mut E) where E: Events;

pub TomlFile: () = <kvs:KeyVal*> <ts:AnyTable*>
    => events.reduce(FILE, kvs.len() + ts.len());

AnyTable = { Table, ArrayTable };

KeyVal: () = Key Eq Val
    => events.reduce(KEY_VAL, 3);

Key = { BareKey, BasicString, LiteralString };

Val = { Literal, Array, Dict };

Literal = {
    Number, Bool, DateTime,
    BasicString, MultilineBasicString,
    LiteralString, MultilineLiteralString,
};

Array: () = L_BRACK <xs:CommaList<Val>> R_BRACK
    => events.reduce(ARRAY, xs + 2);


Dict: () = L_CURLY <xs:CommaList<KeyVal>> R_CURLY
    => events.reduce(DICT, xs + 2);

Table: () = TableHeader <kvs:KeyVal*>
    => events.reduce(TABLE, 1 + kvs.len());

TableHeader: () = L_BRACK Key <keys:(Dot Key)*> R_BRACK
    => events.reduce(TABLE_HEADER, 3 + keys.len() * 2);

ArrayTable: () =  ArrayTableHeader <kvs:KeyVal*>
    => events.reduce(ARRAY_TABLE, 1 + kvs.len());

ArrayTableHeader: () = L_BRACK L_BRACK Key <keys:(Dot Key)*> R_BRACK R_BRACK
    => events.reduce(TABLE_HEADER, 5 + keys.len() * 2);


Eq: () = <l:@L> "=" <r:@R> => events.shift(EQ, l, r);
Dot: () = <l:@L> "." <r:@R> => events.shift(DOT, l, r);
Comma: () = <l:@L> "," <r:@R> => events.shift(COMMA, l, r);
L_BRACK: () = <l:@L> "[" <r:@R> => events.shift(L_BRACK, l, r);
R_BRACK: () = <l:@L> "]" <r:@R> => events.shift(R_BRACK, l, r);
L_CURLY: () = <l:@L> "{" <r:@R> => events.shift(L_CURLY, l, r);
R_CURLY: () = <l:@L> "}" <r:@R> => events.shift(R_CURLY, l, r);
Number: () = {
    <l:@L> number <r:@R> => events.shift(NUMBER, l, r),
    <l:@L> bare_key_or_number <r:@R> => events.shift(NUMBER, l, r),
};
DateTime: () = {
    <l:@L> date_time <r:@R> => events.shift(DATE_TIME, l, r),
    <l:@L> bare_key_or_date <r:@R> => events.shift(DATE_TIME, l, r),
};
Bool: () = <l:@L> bool <r:@R> => events.shift(BOOL, l, r);
BareKey: () = {
    <l:@L> bare_key <r:@R> => events.shift(BARE_KEY, l, r),
    <l:@L> bare_key_or_number <r:@R> => events.shift(BARE_KEY, l, r),
    <l:@L> bare_key_or_date <r:@R> => events.shift(BARE_KEY, l, r),
};

BasicString: () = <l:@L> basic_string <r:@R> =>
    events.shift(BASIC_STRING, l, r);
MultilineBasicString: () = <l:@L> multiline_basic_string <r:@R> =>
    events.shift(MULTILINE_BASIC_STRING, l, r);
LiteralString: () = <l:@L> literal_string <r:@R> =>
    events.shift(LITERAL_STRING, l, r);
MultilineLiteralString: () = <l:@L> multiline_literal_string <r:@R> =>
    events.shift(MULTILINE_LITERAL_STRING, l, r);


CommaList<T>: usize = {
    <v:(<T> Comma)*> <e:T?> => v.len() * 2 + e.into_iter().count(),
};


match {
    r"[0-9]+" => bare_key_or_number,
    r"[0-9]{4}-[0-9]{2}-[0-9]{2}[Zz]?" => bare_key_or_date,

    r#"(?x)
       "
        ([^\r\n"\\] | \\.)*
       "
    "# => basic_string,
    r#"(?x)
       """
        ([^"] | \\. | "[^"] | ""[^"])*
       """
    "# => multiline_basic_string,

    r#"(?x)
       '
        ([^\r\n'] | \\.)*
       '
    "# => literal_string,
    r#"(?x)
       '''
        ([^'] | \\. | '[^'] | ''[^'])*
       '''
    "# => multiline_literal_string,

    r"(false|true)" => bool,
} else {
    r"(?x)
      [-+]?
      (0|[1-9](_?[0-9])*) # no leading zeros
      (\.[0-9](_?[0-9])*)?
      ([eE][-+]?[1-9](_?[0-9])*)?
    " => number,
} else {
    r"(?x)
      ( [0-9]{4}-[0-9]{2}-[0-9]{2} ([Tt]([0-9]{2}:[0-9]{2}:[0-9]{2}(\.[0-9]+)?))?
      | ([0-9]{2}:[0-9]{2}:[0-9]{2}(\.[0-9]+)?))
      ([Zz]|[+-][0-9]{2}:[0-9]{2})?
    " => date_time,
    r"[0-9_\-a-zA-Z]+" => bare_key,
    _,
}

